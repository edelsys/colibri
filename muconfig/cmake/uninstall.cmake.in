if (NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR "Cannot find install manifest: "
                      "\"@CMAKE_BINARY_DIR@/install_manifest.txt\"")
endif (NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")

file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" INSTALL_MANIFEST)
string(REGEX REPLACE "\n" ";" INSTALL_MANIFEST "${INSTALL_MANIFEST}")
list(REVERSE INSTALL_MANIFEST)

foreach (INSTALLED_FILE ${INSTALL_MANIFEST})

  if (NOT MUCONFIG_INCLUDE_INSTALL_ROOT)
    get_filename_component(FILE_NAME ${INSTALLED_FILE} NAME)
    if (FILE_NAME STREQUAL muconfig.h)
      get_filename_component(PARENT_DIR ${INSTALLED_FILE} PATH)
      get_filename_component(PARENT_DIR_NAME ${PARENT_DIR} NAME)
      if (PARENT_DIR_NAME STREQUAL muconfig AND IS_DIRECTORY ${PARENT_DIR})
	      set(MUCONFIG_INCLUDE_INSTALL_ROOT ${PARENT_DIR})
      endif()
    endif()
  endif()

  message(STATUS "Uninstalling \"$ENV{DESTDIR}${INSTALLED_FILE}\"")
  if (EXISTS "$ENV{DESTDIR}${INSTALLED_FILE}")
    execute_process(COMMAND @CMAKE_COMMAND@
                    -E remove "$ENV{DESTDIR}${INSTALLED_FILE}"
                    OUTPUT_VARIABLE RM_OUT
                    RESULT_VARIABLE RM_RETVAL)
    if (NOT ${RM_RETVAL} EQUAL 0)
      message(FATAL_ERROR
	      "Failed to remove \"$ENV{DESTDIR}${INSTALLED_FILE}\"")
    endif()
  else()
    message(STATUS "File \"$ENV{DESTDIR}${INSTALLED_FILE}\" does not exist.")
  endif()

endforeach()

if (MUCONFIG_INCLUDE_INSTALL_ROOT AND EXISTS ${MUCONFIG_INCLUDE_INSTALL_ROOT})
  message(STATUS "Removing muconfig install directory: "
	  "\"$ENV{DESTDIR}${MUCONFIG_INCLUDE_INSTALL_ROOT}\"")
  execute_process(COMMAND @CMAKE_COMMAND@
                  -E remove_directory
		  "$ENV{DESTDIR}${MUCONFIG_INCLUDE_INSTALL_ROOT}"
                  OUTPUT_VARIABLE RM_OUT
                  RESULT_VARIABLE RM_RETVAL)
  if (NOT ${RM_RETVAL} EQUAL 0)
    message(FATAL_ERROR
	    "Failed to remove: \"$ENV{DESTDIR}${MUCONFIG_INCLUDE_INSTALL_ROOT\"")
  endif()
else()
  message(FATAL_ERROR "Failed to find muconfig installed include directory "
                      "(e.g. /usr/local/include/muconfig), candidate: "
		      "\"$ENV{DESTDIR}${MUCONFIG_INCLUDE_INSTALL_ROOT}\"")
endif()

